// prucode.p

.origin 0
.entrypoint START

#include "prucode.hp"

#define GPIO1 0x4804c000
#define GPIO_CLEARDATAOUT 0x190
#define GPIO_SETDATAOUT 0x194
#define DELAY_MILLIS 50  // reminder: 1,000 millis in a sec
#define CLOCK 200000  // 200 mhz clock / 1000 millis
#define CLOCKS_PER_LOOP 2 // Number of clock cycles per loop
#define DELAYCOUNT DELAY_MILLIS * CLOCK / CLOCKS_PER_LOOP


START:

    // Enable OCP master port
    LBCO      r0, CONST_PRUCFG, 4, 4
    CLR     r0, r0, 4         // Clear SYSCFG[STANDBY_INIT] to enable OCP master port
    SBCO      r0, CONST_PRUCFG, 4, 4

    // Configure the programmable pointer register for PRU0 by setting c28_pointer[15:0]
    // field to 0x0120.  This will make C28 point to 0x00012000 (PRU shared RAM).
    MOV     r0, 0x00000120
    MOV       r1, CTPPR_0
    ST32      r0, r1

    // Configure the programmable pointer register for PRU0 by setting c31_pointer[15:0]
    // field to 0x0010.  This will make C31 point to 0x80001000 (DDR memory).
    MOV     r0, 0x00100000
    MOV       r1, CTPPR_1
    ST32      r0, r1

    //Load values from external DDR Memory into Registers R0/R1/R2
    // Load 12 bytes into r0 from (CONST_DDR + 0)
    LBCO      r0, CONST_DDR, 0, 12

    //Store values from read from the DDR memory into PRU shared RAM
    // Store 12 bytes into r0 from (CONST_PRUSHAREDRAM + 0)
    SBCO      r0, CONST_PRUSHAREDRAM, 0, 12

    // LOOP1 counter
    MOV r1, 100
LOOP1:
    // Turn led on -- USR0..3 LEDS are GPIO1_21..24
    MOV r2, 1<<21  // set only 21st bit (usr0)
    MOV r3, GPIO1 | GPIO_SETDATAOUT  // Set GPIO1 address
    SBBO r2, r3, 0, 4  // copy contents of r2 to address stored at r3

    // initialize delay counter
    MOV r0, DELAYCOUNT
DEL_ON:
    // delay: decrement r0 while r0 != 0
    SUB r0, r0, 1
    QBNE DEL_ON, r0, 0

    // Turn led off
    MOV R2, 1<<21
    MOV r3, GPIO1 | GPIO_CLEARDATAOUT
    SBBO r2, r3, 0, 4

    MOV r0, DELAYCOUNT
DEL_OFF:
    SUB r0, r0, 1
    QBNE DEL_OFF, r0, 0

    SUB r1, r1, 1  // decrement r1 (loop counter)
    QBNE LOOP1, r1, 0  // goto loop1 if r1 != 0

    // Send notification to Host for program completion
    MOV       r31.b0, PRU0_ARM_INTERRUPT+16

    // Halt the processor
    HALT

